// Author: yuce
// Created on: 2020-07-31, at: 23:50 +0300

plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'me.champeau.gradle.jmh' version '0.5.0'
}

ext {
    junitVersion = '5.6.2'
    jacksonVersion = '2.11.1'
    jmhVersion = '1.22'
}

group 'net.scaleplan'
version '0.2.3'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compileOnly "org.jetbrains:annotations:17.0.0"

    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        implementation 'javax.annotation:javax.annotation-api:1.3.2'
    }

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.jetbrains:annotations:17.0.0"
}

test {
    useJUnitPlatform()
}

jmh {
    duplicateClassesStrategy = 'warn'
    jmhVersion = '1.22'
    profilers = ['perf']
}

publishing {
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = 'https://maven.pkg.github.com/scale-plan/maven'
            credentials {
                username "$scaleplan_user"
                password "$scaleplan_pass"
            }
        }
    }
}
